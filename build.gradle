/*
 * Copyright 2015-2020 Real Logic Limited.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'java-library'
    id 'idea'
    id 'checkstyle'
    id 'com.github.johnrengelman.shadow' version '5.2.0'
    id 'com.github.ben-manes.versions' version '0.28.0'
}

defaultTasks 'clean', 'build'

repositories {
    mavenLocal()
    mavenCentral()
}

def agronaVersion = '1.4.0'
def aeronVersion = '1.26.0'
def jmhVersion = '1.23'
def hdrHistogramVersion = '2.1.12'
def disruptorVersion = '3.4.2'
def junitVersion = '5.6.1'
def mockitoVersion = '3.3.3'
def checkstyleVersion = '8.28'

checkstyle.toolVersion = "${checkstyleVersion}"

dependencies {
    api "org.agrona:agrona:${agronaVersion}"
    
    annotationProcessor "org.openjdk.jmh:jmh-generator-annprocess:${jmhVersion}"
    implementation "org.openjdk.jmh:jmh-core:${jmhVersion}"
    implementation "io.aeron:aeron-archive:${aeronVersion}"
    implementation "org.hdrhistogram:HdrHistogram:${hdrHistogramVersion}"
    implementation "com.lmax:disruptor:${disruptorVersion}"

    testImplementation "org.mockito:mockito-junit-jupiter:${mockitoVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter-params:${junitVersion}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
}

tasks.withType(JavaCompile) {
    if (JavaVersion.current().isJava9Compatible()) {
        options.compilerArgs.addAll(['--add-exports', 'jdk.unsupported/sun.misc=ALL-UNNAMED'])
    }
    options.compilerArgs.add('-XDignore.symbol.file') // Suppress warnings about using Unsafe
    options.fork = true
    options.forkOptions.javaHome = file("${System.env.JAVA_HOME}")
    options.encoding = 'UTF-8'
    options.deprecation = true
}

test {
    useJUnitPlatform()
}

jar.enabled = false
jar.finalizedBy shadowJar

shadowJar {
    archiveFileName = 'benchmarks.jar'
    manifest.attributes('Main-Class': 'org.openjdk.jmh.Main')
}

task runJavaBenchmarks(type: Exec) {
    commandLine 'java',
    '-jar', 'build/libs/benchmarks.jar',
    '-jvmArgs', '-Dagrona.disable.bounds.checks=true -XX:+UseParallelOldGC',
    '-wi', '3', '-w', '1s', '-i', '5', '-r', '1s', '-tu', 'ns', '-f', '5'
}

task runAeronJavaBenchmarks(type: Exec) {
    commandLine 'java',
    '-jar', 'build/libs/benchmarks.jar',
    '-jvmArgs', '-Dagrona.disable.bounds.checks=true -XX:+UseParallelOldGC',
    'Aeron',
    '-wi', '3', '-w', '1s', '-i', '5', '-r', '1s', '-tu', 'ns', '-f', '5'
}

wrapper {
    gradleVersion = '6.3'
    distributionType = 'ALL'
}